(defun caar (x)
  (car (car x)))
(defun cadr (x)
  (car (cdr x)))
(defun cdar (x)
  (cdr (car x)))
(defun cddr (x)
  (cdr (cdr x)))

(defun caaar (x)
  (car (car (car x))))
(defun caadr (x)
  (car (car (cdr x))))
(defun cadar (x)
  (car (cdr (car x))))
(defun caddr (x)
  (car (cdr (cdr x))))
(defun cdaar (x)
  (cdr (car (car x))))
(defun cdadr (x)
  (cdr (car (cdr x))))
(defun cddar (x)
  (cdr (cdr (car x))))
(defun cdddr (x)
  (cdr (cdr (cdr x))))

(defun caaaar (x)
  (car (car (car (car x)))))
(defun caaadr (x)
  (car (car (car (cdr x)))))
(defun caadar (x)
  (car (car (cdr (car x)))))
(defun caaddr (x)
  (car (car (cdr (cdr x)))))
(defun cadaar (x)
  (car (cdr (car (car x)))))
(defun cadadr (x)
  (car (cdr (car (cdr x)))))
(defun caddar (x)
  (car (cdr (cdr (car x)))))
(defun cadddr (x)
  (car (cdr (cdr (cdr x)))))
(defun cdaaar (x)
  (cdr (car (car (car x)))))
(defun cdaadr (x)
  (cdr (car (car (cdr x)))))
(defun cdadar (x)
  (cdr (car (cdr (car x)))))
(defun cdaddr (x)
  (cdr (car (cdr (cdr x)))))
(defun cddaar (x)
  (cdr (cdr (car (car x)))))
(defun cddadr (x)
  (cdr (cdr (car (cdr x)))))
(defun cdddar (x)
  (cdr (cdr (cdr (car x)))))
(defun cddddr (x)
  (cdr (cdr (cdr (cdr x)))))


(defun null (x)
  (eq '() x))

(defun and. (x y)
  (if x
      (if y
          t)))

(defun not. (x)
  (if x
      nil
      t))

(defun append. (x y)
  (if (not. (null x))
      (cons (car x) (append. (cdr x) y))
      y))

(defun pair. (x y)
  (cond ((and. (null x) (null y)) nil)
        ((and. (not. (atom x)) (not. (atom y)))
         (cons (list (car x) (car y))
               (pair. (cdr x) (cdr y))))))

(defun subst (x y z)
  (if (atom z)
      (if (eq z y)
          x
          z)
      (cons (subst x y (car z))
            (subst x y (cdr z)))))

(defun assoc. (x y)
  (if (eq (caar y) x)
      (cadar y)
      (assoc. x (cdr y))))

(extern eval. (e a))

(defun evcon. (c a)
  (if (eval. (caar c) a)
      (eval. (cadar c) a)
      (evcon. (cdr c) a)))

(defun evlis. (m a)
  (if (null m) nil
      (cons (eval. (car m) a)
            (evlis. (cdr m) a))))

(defun eval. (e a)
  (cond ((atom e) (assoc. e a))
        ((atom (car e)) (cond ((eq 'quote (car e)) (cadr e))
			      ((eq 'atom (car e)) (atom (eval. (cadr e) a)))
			      ((eq 'eq (car e)) (eq (eval. (cadr e) a)
						    (eval. (caddr e) a)))
			      ((eq 'car (car e)) (car (eval. (cadr e) a)))
			      ((eq 'cdr (car e)) (cdr (eval. (cadr e) a)))
			      ((eq 'cons (car e)) (cons (eval. (cadr e) a)
							(eval. (caddr e) a)))
			      ((eq 'cond (car e)) (evcon. (cdr e) a))
			      (t (eval. (cons (assoc. (car e) a)
					      (cdr e))
					a))))
	((eq (caar e) 'label) (eval. (cons (caddar e) (cdr e))
				     (cons (list (cadar e) (car e)) a)))
	((eq (caar e) 'lambda) (eval. (caddar e)
				      (append. (pair. (cadar e) (evlis. (cdr e) a))
					       a)))))
	 


